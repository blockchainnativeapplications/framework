buildscript {
    repositories { jcenter() }

    dependencies {
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:2.2.+'
    }
}

apply plugin: 'nebula-aggregate-javadocs'

tasks.withType(Javadoc) {
    options.addBooleanOption('html5', true)
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group 'org.blockchainnative'
    version '1.1.0'

    compileJava {
        options.compilerArgs << '-parameters'
    }

    compileTestJava {
        options.compilerArgs << '-parameters'
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnit {
            excludeCategories 'org.blockchainnative.test.IntegrationTest'
        }
    }

    task integrationTest(type: Test) {
        useJUnit {
            includeCategories 'org.blockchainnative.test.IntegrationTest'
        }
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }

    targetCompatibility = 1.10
    sourceCompatibility = 1.10

    ext {
        springBootVersion = '2.0.5.RELEASE'
        rxJavaVersion = '2.2.1'
        web3jVersion = '4.0.3'
        quorumVersion = '4.0.3'
    }

    javadoc {
        options.addBooleanOption('html5', true)
    }

    publishing {
        repositories {
            maven {
                name = 'local'
                url = "file://${getRootProject().getBuildDir()}/repository"
            }
        }
    }

    dependencies {
        compile group: 'net.bytebuddy', name: 'byte-buddy', version: '1.8.17'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.25'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.0'
    }
}